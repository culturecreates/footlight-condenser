PREFIX schema: <http://schema.org/>
PREFIX adr: <http://kg.artsdata.ca/resource/>
delete {
      ?uri  schema:startDate ?dates ;
       a schema:Event .
}
insert {
    ?uri schema:startDate ?firstDate ;
         a schema:EventSeries ;
         schema:subEvent ?subEventUri .
           ?subEventUri a schema:Event  ;
              schema:name ?name ;
              schema:startDate ?dates ;
              schema:location ?location .
}
where {
    ?uri schema:location ?location ;
         schema:name ?name .
    {
        select ?dates ?uri where {
            values ?uri { <http://kg.artsdata.ca/resource/spec-qc-ca_broue> }
            ?uri  schema:startDate ?dates
        }
    }
    {
        select ?firstDate where {
            values ?uri { <http://kg.artsdata.ca/resource/spec-qc-ca_broue> }
            ?uri  schema:startDate ?firstDate
        }  order by ?firstDate limit 1 
    } 
    bind(URI(CONCAT(str(?uri),REPLACE(str(?dates),":",""))) as ?subEventUri)
}